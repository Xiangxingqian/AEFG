public class com.example.qian.HahaActivity extends android.app.Activity
{
    public static java.util.LinkedList activities;
    public static java.util.LinkedList unVisitedActivities;
    public static java.util.LinkedList viewLinkedList;
    public static java.util.LinkedList listenerLinkedList;
    public static boolean isMyEvent;

    public void <init>()
    {
        com.example.qian.HahaActivity $r0;

        $r0 := @this: com.example.qian.HahaActivity;
        specialinvoke $r0.<android.app.Activity: void <init>()>();
        return;
    }

    public static android.content.Intent createIntent(android.app.Activity)
    {
        android.app.Activity $r0;
        android.content.Intent $r1;

        $r0 := @parameter0: android.app.Activity;
        $r1 = new android.content.Intent;
        specialinvoke $r1.<android.content.Intent: void <init>(android.content.Context,java.lang.Class)>($r0, class "com/example/qian/ListViewActivity");
        return $r1;
    }

    public android.content.Intent create(int)
    {
        com.example.qian.HahaActivity $r0;
        int $i0;
        android.content.Intent $r1;

        $r0 := @this: com.example.qian.HahaActivity;
        $i0 := @parameter0: int;
        $r1 = new android.content.Intent;
        specialinvoke $r1.<android.content.Intent: void <init>()>();
        if $i0 <= 0 goto label0;

        virtualinvoke $r1.<android.content.Intent: android.content.Intent setAction(java.lang.String)>("com");
        return $r1;

     label0:
        virtualinvoke $r1.<android.content.Intent: android.content.Intent setAction(java.lang.String)>("org");
        return $r1;
    }

    protected void onCreate(android.os.Bundle)
    {
        com.example.qian.HahaActivity $r0;
        android.os.Bundle $r1;
        android.view.View $r2;
        android.widget.TextView $r4;
        android.widget.Button $r5;
        com.example.qian.HahaActivity$1 $r6;

        $r0 := @this: com.example.qian.HahaActivity;
        $r1 := @parameter0: android.os.Bundle;
        specialinvoke $r0.<android.app.Activity: void onCreate(android.os.Bundle)>($r1);
        virtualinvoke $r0.<com.example.qian.HahaActivity: void CheckEvent()>();
        virtualinvoke $r0.<com.example.qian.HahaActivity: void setContentView(int)>(2130903040);
        virtualinvoke $r0.<com.example.qian.HahaActivity: void setTitle(java.lang.CharSequence)>("haha");
        $r2 = virtualinvoke $r0.<com.example.qian.HahaActivity: android.view.View findViewById(int)>(2131296256);
        $r4 = (android.widget.TextView) $r2;
        $r2 = virtualinvoke $r0.<com.example.qian.HahaActivity: android.view.View findViewById(int)>(2131296257);
        $r5 = (android.widget.Button) $r2;
        $r6 = new com.example.qian.HahaActivity$1;
        specialinvoke $r6.<com.example.qian.HahaActivity$1: void <init>(com.example.qian.HahaActivity)>($r0);
        virtualinvoke $r0.<com.example.qian.HahaActivity: void doReflect(android.view.View,java.lang.Object)>($r5, $r6);
        virtualinvoke $r5.<android.widget.Button: void setOnClickListener(android.view.View$OnClickListener)>($r6);
        virtualinvoke $r0.<com.example.qian.HahaActivity: void runMyThread()>();
        return;
    }

    static void <clinit>()
    {
        java.util.LinkedList linkedList;

        linkedList = new java.util.LinkedList;
        specialinvoke linkedList.<java.util.LinkedList: void <init>()>();
        <com.example.qian.HahaActivity: java.util.LinkedList viewLinkedList> = linkedList;
        linkedList = new java.util.LinkedList;
        specialinvoke linkedList.<java.util.LinkedList: void <init>()>();
        <com.example.qian.HahaActivity: java.util.LinkedList listenerLinkedList> = linkedList;
        <com.example.qian.HahaActivity: boolean isMyEvent> = 0;
        linkedList = <com.example.qian.MainActivity: java.util.LinkedList unVisitedActivities>;
        <com.example.qian.HahaActivity: java.util.LinkedList unVisitedActivities> = linkedList;
        linkedList = <com.example.qian.MainActivity: java.util.LinkedList activities>;
        <com.example.qian.HahaActivity: java.util.LinkedList activities> = linkedList;
        return;
    }

    public void onRestart()
    {
        com.example.qian.HahaActivity base;
        android.content.Intent intent;
        java.util.LinkedList linkedList;
        boolean flag;
        java.lang.Object object;
        java.lang.Class classLocal;

        base := @this: com.example.qian.HahaActivity;
        specialinvoke base.<android.app.Activity: void onRestart()>();
        linkedList = <com.example.qian.HahaActivity: java.util.LinkedList unVisitedActivities>;
        flag = virtualinvoke linkedList.<java.util.AbstractCollection: boolean isEmpty()>();
        if flag != 0 goto label0;

        intent = new android.content.Intent;
        linkedList = <com.example.qian.HahaActivity: java.util.LinkedList unVisitedActivities>;
        object = virtualinvoke linkedList.<java.util.LinkedList: java.lang.Object poll()>();
        classLocal = (java.lang.Class) object;
        specialinvoke intent.<android.content.Intent: void <init>(android.content.Context,java.lang.Class)>(base, classLocal);
        virtualinvoke intent.<android.content.Intent: android.content.Intent putExtra(java.lang.String,java.lang.String)>("qian", "OK");
        virtualinvoke base.<android.app.Activity: void startActivity(android.content.Intent)>(intent);

     label0:
        return;
    }

    public void doTest()
    {
        com.example.qian.HahaActivity activity;
        java.lang.Object object;
        android.view.View view;
        android.widget.Toast toast;
        int int0, int1;
        boolean flag;
        java.lang.Class classLocal;
        java.lang.reflect.Method reflectMethod;
        java.util.LinkedList linkedList;
        java.lang.String string;
        java.lang.Exception exception;
        java.lang.String[] arrayObject;
        java.lang.reflect.Method[] arrayReflectMethod;

        activity := @this: com.example.qian.HahaActivity;

     label0:
        linkedList = <com.example.qian.HahaActivity: java.util.LinkedList viewLinkedList>;
        flag = virtualinvoke linkedList.<java.util.AbstractCollection: boolean isEmpty()>();
        if flag == 0 goto label1;

        toast = staticinvoke <android.widget.Toast: android.widget.Toast makeText(android.content.Context,java.lang.CharSequence,int)>(activity, "Test finishes", 0);
        virtualinvoke toast.<android.widget.Toast: void show()>();
        return;

     label1:
        linkedList = <com.example.qian.HahaActivity: java.util.LinkedList viewLinkedList>;
        object = virtualinvoke linkedList.<java.util.LinkedList: java.lang.Object poll()>();
        view = (android.view.View) object;
        linkedList = <com.example.qian.HahaActivity: java.util.LinkedList listenerLinkedList>;
        object = virtualinvoke linkedList.<java.util.LinkedList: java.lang.Object poll()>();
        classLocal = virtualinvoke object.<java.lang.Object: java.lang.Class getClass()>();
        arrayReflectMethod = virtualinvoke classLocal.<java.lang.Class: java.lang.reflect.Method[] getMethods()>();
        int0 = 0;

     label2:
        int1 = lengthof arrayReflectMethod;
        if int0 >= int1 goto label0;

        reflectMethod = arrayReflectMethod[int0];
        string = virtualinvoke reflectMethod.<java.lang.reflect.Method: java.lang.String getName()>();
        flag = virtualinvoke string.<java.lang.String: boolean startsWith(java.lang.String)>("on");
        if flag == 0 goto label6;

     label3:
        arrayObject = newarray (java.lang.Object)[1];
        arrayObject[0] = view;
        virtualinvoke reflectMethod.<java.lang.reflect.Method: java.lang.Object invoke(java.lang.Object,java.lang.Object[])>(object, arrayObject);

     label4:
        goto label0;

     label5:
        exception := @caughtexception;
        virtualinvoke exception.<java.lang.Throwable: void printStackTrace()>();

     label6:
        int0 = int0 + 1;
        goto label2;

        catch java.lang.Exception from label3 to label4 with label5;
    }

    public void runMyThread()
    {
        com.example.qian.HahaActivity base;
        com.example.qian.TestThread testThread;
        com.example.qian.FinishThread finishThread;
        java.lang.Class classLocal;
        java.util.LinkedList linkedList1;
        boolean flag;

        base := @this: com.example.qian.HahaActivity;
        flag = <com.example.qian.HahaActivity: boolean isMyEvent>;
        if flag == 0 goto label1;

        testThread = new com.example.qian.TestThread;
        specialinvoke testThread.<com.example.qian.TestThread: void <init>(java.lang.Object)>(base);
        virtualinvoke testThread.<java.lang.Thread: void start()>();

     label0:
        return;

     label1:
        classLocal = virtualinvoke base.<java.lang.Object: java.lang.Class getClass()>();
        linkedList1 = <com.example.qian.HahaActivity: java.util.LinkedList activities>;
        flag = virtualinvoke linkedList1.<java.util.LinkedList: boolean contains(java.lang.Object)>(classLocal);
        if flag != 0 goto label2;

        linkedList1 = <com.example.qian.HahaActivity: java.util.LinkedList activities>;
        virtualinvoke linkedList1.<java.util.LinkedList: boolean offer(java.lang.Object)>(classLocal);
        linkedList1 = <com.example.qian.HahaActivity: java.util.LinkedList unVisitedActivities>;
        virtualinvoke linkedList1.<java.util.LinkedList: boolean offer(java.lang.Object)>(classLocal);

     label2:
        finishThread = new com.example.qian.FinishThread;
        specialinvoke finishThread.<com.example.qian.FinishThread: void <init>(java.lang.Object)>(base);
        virtualinvoke finishThread.<java.lang.Thread: void start()>();
        goto label0;
    }

    public void CheckEvent()
    {
        com.example.qian.HahaActivity base;
        android.os.Bundle bundle;
        android.content.Intent intent;
        java.lang.Object object;
        boolean flag;

        base := @this: com.example.qian.HahaActivity;
        intent = virtualinvoke base.<android.app.Activity: android.content.Intent getIntent()>();
        bundle = virtualinvoke intent.<android.content.Intent: android.os.Bundle getExtras()>();
        if bundle == null goto label0;

        object = virtualinvoke bundle.<android.os.Bundle: java.lang.Object get(java.lang.String)>("qian");
        if object == null goto label0;

        flag = virtualinvoke object.<java.lang.Object: boolean equals(java.lang.Object)>("OK");
        if flag == 0 goto label0;

        <com.example.qian.HahaActivity: boolean isMyEvent> = 1;

     label0:
        return;
    }

    public void doReflect(android.view.View, java.lang.Object)
    {
        com.example.qian.HahaActivity activity;
        java.lang.Object object1, object2;
        android.view.View view;
        boolean flag;
        java.lang.Class classLocal;
        java.util.LinkedList linkedList1, linkedList2;
        java.lang.String string;
        java.lang.Exception exception;
        java.lang.reflect.Field field1, field2, field3;
        java.lang.Boolean booleanLocal;
        android.content.Context context;

        activity := @this: com.example.qian.HahaActivity;
        view := @parameter0: android.view.View;
        object1 := @parameter1: java.lang.Object;
        context = virtualinvoke view.<android.view.View: android.content.Context getContext()>();
        classLocal = virtualinvoke context.<java.lang.Object: java.lang.Class getClass()>();
        string = virtualinvoke classLocal.<java.lang.Class: java.lang.String getName()>();

     label0:
        classLocal = staticinvoke <java.lang.Class: java.lang.Class forName(java.lang.String)>(string);
        field1 = virtualinvoke classLocal.<java.lang.Class: java.lang.reflect.Field getField(java.lang.String)>("viewLinkedList");
        classLocal = staticinvoke <java.lang.Class: java.lang.Class forName(java.lang.String)>(string);
        field2 = virtualinvoke classLocal.<java.lang.Class: java.lang.reflect.Field getField(java.lang.String)>("listenerLinkedList");
        classLocal = staticinvoke <java.lang.Class: java.lang.Class forName(java.lang.String)>(string);
        field3 = virtualinvoke classLocal.<java.lang.Class: java.lang.reflect.Field getField(java.lang.String)>("isMyEvent");
        object2 = virtualinvoke field1.<java.lang.reflect.Field: java.lang.Object get(java.lang.Object)>(null);
        linkedList1 = (java.util.LinkedList) object2;
        object2 = virtualinvoke field2.<java.lang.reflect.Field: java.lang.Object get(java.lang.Object)>(null);
        linkedList2 = (java.util.LinkedList) object2;
        object2 = virtualinvoke field3.<java.lang.reflect.Field: java.lang.Object get(java.lang.Object)>(null);
        booleanLocal = (java.lang.Boolean) object2;
        flag = virtualinvoke booleanLocal.<java.lang.Boolean: boolean booleanValue()>();
        if flag == 0 goto label1;

        virtualinvoke linkedList1.<java.util.LinkedList: boolean offer(java.lang.Object)>(view);
        virtualinvoke linkedList2.<java.util.LinkedList: boolean offer(java.lang.Object)>(object1);

     label1:
        return;

     label2:
        exception := @caughtexception;
        virtualinvoke exception.<java.lang.Throwable: void printStackTrace()>();
        goto label1;

        catch java.lang.Exception from label0 to label1 with label2;
    }
}
